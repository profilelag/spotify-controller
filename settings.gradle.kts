pluginManagement {
	repositories {
		maven("https://maven.fabricmc.net/")
		mavenCentral()
        maven("https://repo.essential.gg/repository/maven-public")
        maven("https://maven.architectury.dev")
        maven("https://maven.minecraftforge.net")
		gradlePluginPortal()
	}
    plugins {
        val egtVersion = "0.7.0-alpha.4" // should be whatever is displayed in above badge
        id("gg.essential.multi-version.root") version egtVersion
        id("gg.essential.multi-version.api-validation") version egtVersion
    }
}

val versions = listOf(
    "1.21.1-fabric",
    "1.21.3-fabric",
    "1.21.4-fabric",
    "1.21.5-fabric",
    "1.21.8-fabric",
    "1.21.9-fabric",
    "1.21.11-fabric",
    "26.1-fabric"
)

val noMappings = listOf(
    "26.1-fabric"
)

versions.forEach { version ->
    include(":$version")
    project(":$version").apply {
        // This is where the `build` folder and per-version overwrites will reside
        projectDir = file("versions/$version")
        // All sub-projects get configured by the same `build.gradle.kts` file, the string is relative to projectDir
        // You could use separate build files for each project, but usually that would just be duplicating lots of code
        buildFileName = if (noMappings.contains(version)) "../../build_new.gradle.kts" else "../../build.gradle.kts"

    }
}

// We use the `build.gradle.kts` file for all the sub-projects (cause that's where most the interesting stuff lives),
// so we need to use a different build file for the original root project.
rootProject.buildFileName = "root.gradle.kts"
